buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url 'http://repo.spring.io/plugins-release' }
  }
  dependencies {
    classpath("org.springframework.build.gradle:propdeps-plugin:${propdepsVersion}")
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath("org.kt3k.gradle.plugin:coveralls-gradle-plugin:${coverallsGradlePluginVersion}")
  }
}

plugins {
  id 'net.ltgt.apt' version '0.9'
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'propdeps'


group = 'faforever'
version = '1.1.2'

sourceCompatibility = 1.9
targetCompatibility = 1.9

repositories {
  mavenCentral()
  jcenter()
  maven { url 'https://jitpack.io' }
  maven { url "http://dl.bintray.com/typesafe/maven-releases" }
  // TODO remove when spring-boot-admin-client 2.0.0 has been released
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
  compile.exclude module: "spring-boot-starter-tomcat"
  compile.exclude module: "assertj-core"
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6'
}

compileJava.dependsOn(processResources)
processResources {
  filesMatching('**/application.yml') {
    filter {
      it.replace('#faf-server.version#', project.version.toString())
    }
  }
}

// JACOCO & COVERALLS

apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
jacocoTestReport {
  reports {
    xml.enabled = true
  }
}

// CODACY

configurations {
  codacy
}

// TODO Codacy coverage reporter does not yet support Java 9. See https://github.com/codacy/codacy-coverage-reporter/issues/76
task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
  main = "com.codacy.CodacyCoverageReporter"
  classpath = configurations.codacy
  args = ["-l", "Java", "-r", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"]
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-data-jpa")
  compile("org.springframework.boot:spring-boot-starter-cache")
  compile("org.springframework.boot:spring-boot-starter-integration")
  compile("org.springframework.boot:spring-boot-starter-websocket")
  compile("org.springframework.boot:spring-boot-starter-security")
  compile("org.springframework.boot:spring-boot-starter-undertow")
  compile("org.springframework.security:spring-security-messaging")
  compile("org.springframework.security:spring-security-jwt:${springSecurityJwtVersion}")
  compile("org.springframework.security.oauth.boot:spring-security-oauth2-autoconfigure:${springSecurityOauth2AutoconfigureVersion}")
  compile("org.springframework:spring-context-support")
  compile("org.springframework.integration:spring-integration-ip")
  compile("org.springframework.integration:spring-integration-security")
  compile("org.springframework.integration:spring-integration-websocket")
  compile("org.springframework.integration:spring-integration-event")
  compile("com.github.ben-manes.caffeine:caffeine")
  compile("org.jolokia:jolokia-core")
  compile("de.codecentric:spring-boot-admin-starter-client:${springBootAdminClientVersion}")
  compile("io.micrometer:micrometer-registry-prometheus:${micrometerRegistryPrometheusVersion}")
  compile("de.gesundkrank.jskills:jskills:${jSkillsVersion}")
  compile("com.github.nocatch:nocatch:${noCatchVersion}")
  compile("org.jetbrains:annotations:${jetbrainsAnnotationsVersion}")
  compile("com.google.guava:guava:${guavaVersion}")
  compile("javax.annotation:javax.annotation-api:${javaxAnnotationApiVersion}")
  compile("javax.inject:javax.inject:${javaxInjectVersion}")
  // When switching from Java 8 to 9, I got "class file for javax.interceptor.interceptorbinding not found". Adding this fixed it, but IDK what caused it.
  compile("javax.interceptor:javax.interceptor-api:${javaxInterceptorApiVersion}")
  compile("com.github.jasminb:jsonapi-converter:${jsonApiConverterVersion}")
  compile("com.twilio.sdk:twilio:${twilioVersion}")
  compile("org.mapstruct:mapstruct-jdk8:${mapstructVersion}")
  compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeJsr310Version}")
  compile("javax.xml.bind:jaxb-api:${jaxbVersion}")
  compile("com.sun.xml.bind:jaxb-ri:${jaxbVersion}")
  compile("com.sun.xml.bind:jaxb-impl:${jaxbVersion}")
  compile("com.sun.xml.bind:jaxb-core:${jaxbVersion}")
  compile("javax.activation:activation:${javaxActivationVersion}")

  compile("com.maxmind.geoip2:geoip2:${geoip2Version}") {
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
  }

  runtime("mysql:mysql-connector-java:${mysqlConnectorVersion}")

  optional("org.springframework.boot:spring-boot-devtools")
  optional("org.springframework.boot:spring-boot-configuration-processor")
  optional("org.projectlombok:lombok:${lombokVersion}")
  optional("org.mapstruct:mapstruct-processor:${mapstructVersion}")

  testCompile("org.springframework.boot:spring-boot-starter-test")
  testCompile("com.googlecode.zohhak:zohhak:${zohhakVersion}")

  codacy("com.github.codacy:codacy-coverage-reporter:-SNAPSHOT")

  apt("org.mapstruct:mapstruct-processor:${mapstructVersion}")
  apt("org.projectlombok:lombok:${lombokVersion}")
}
